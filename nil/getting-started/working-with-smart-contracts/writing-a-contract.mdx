# Writing a contract

This tutorial explains how to create smart contracts that take full advantage of the unique features of =nil;.

## Prerequisites

Complete the following tutorials before proceeding.

* [**Creating a wallet**](../essentials/creating-a-wallet)
* [**Handling async execution**](../essentials/handling-async-execution)
* [**Receiving external messages**](../essentials/receiving-ext-messages)

## Specification

The tutorial defines two contracts.

Contract 1 is a retailer. The contract can call a special 'manufacturer' contract and order new products by performing an async call.

Contract 2 is the manufacturer. It has the following features:

* It can [**accept external messages and verify them**](../essentials/receiving-ext-messages)
* It will [**limit what messages can call**](../../tools/sol-library/func-modifiers) some of its functions

## Creating the first contract

The `Retailer` contract (Contract 1) orders products by performing an async call to the specified address of the manufacturer contract:

```solidity showLineNumbers
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "./Nil.sol";

contract Retailer {
    using Nil for address;

    receive() external payable {}

    function orderProduct(address dst, string calldata productName) public {
        dst.asyncCall(
            msg.sender,
            msg.sender,
            100000,
            false,
            100000 * 10,
            abi.encodeWithSignature("createProduct(string)", productName)
        );
    }

    function verifyExternal(
        uint256 messageHash,
        bytes memory authData
    ) external view returns (bool) {
        return true;
    }
}

```

## Creating the second contract

Contract 2 has some basic architecture in place representing products:

```solidity showLineNumbers
pragma solidity ^0.8.0;

import "./Nil.sol";

contract Manufacturer {
    using Nil for address;

    struct Product {
        uint id;
        string name;
    }

    mapping(uint => Product) public products;
    uint public nextProductId;
}
```

Contract 1 stores its owner's `pubkey`, which is useful for verifying external messages. The contract also contains the address of the retailer contract. This is done so that only the retailer contract can order new products.

```solidity showLineNumbers
    bytes pubkey;
    address retailerContractAddress;

    constructor(
        bytes memory _pubkey,
        address _retailerContractAddress
    ) payable {
        pubkey = _pubkey;
        retailerContractAddress = _retailerContractAddress;
    }

    receive() external payable {}

```

The contract also conntains a simple mechanism for verifying external messages:

```solidity showLineNumbers
contract Manufacturer {

    function verifyExternal(uint256 messageHash, bytes memory authData) external view returns (bool) {
        if (isRetailerAddressSet == false) {
            if (Nil.validateSignature(pubkey, messageHash, authData)) {
                return true;
            }
        }
        return false;
    }

}
```

Last but not least, Contract 2 can produce new products (but only via an internal message sent from the retailer):

```solidity showLineNumbers
function createProduct(string calldata name) public onlyInternal {
        if (msg.sender == retailerContractAddress) {
            products[nextProductId] = Product(nextProductId, name);
            nextProductId++;
        }  
    }
```

Contract 2 can also output the products that have been ordered by the retailer:

```solidity showLineNumbers
function getProducts() public view returns (uint[] memory, string[] memory) {
        uint[] memory ids = new uint[](nextProductId);
        string[] memory names = new string[](nextProductId);

        for (uint i = 0; i < nextProductId; i++) {
            Product storage product = products[i];
            ids[i] = product.id;
            names[i] = product.name;
        }

        return (ids, names);
    }
```

Here is the full code of Contract 2:

```solidity showLineNumbers
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "./Nil.sol";

contract Manufacturer is NilBase {
    using Nil for address;

    bytes pubkey;
    address retailerContractAddress;

    constructor(
        bytes memory _pubkey,
        address _retailerContractAddress
    ) payable {
        pubkey = _pubkey;
        retailerContractAddress = _retailerContractAddress;
    }

    receive() external payable {}

    struct Product {
        uint id;
        string name;
    }

    mapping(uint => Product) public products;
    uint public nextProductId;

    function createProduct(
        string calldata name
    ) public onlyInternal returns (bool) {
        if (msg.sender == retailerContractAddress) {
            products[nextProductId] = Product(nextProductId, name);
            nextProductId++;
            return true;
        }
        return false;
    }

    function verifyExternal(
        uint256 hash,
        bytes calldata signature
    ) external view returns (bool) {
        return Nil.validateSignature(pubkey, hash, signature);
    }

    function getProducts()
        public
        view
        returns (uint[] memory, string[] memory)
    {
        uint[] memory ids = new uint[](nextProductId);
        string[] memory names = new string[](nextProductId);

        for (uint i = 0; i < nextProductId; i++) {
            Product storage product = products[i];
            ids[i] = product.id;
            names[i] = product.name;
        }

        return (ids, names);
    }
}
```
## Compiling the contracts

Compile Contract 1 with:

```bash
solc ./Retailer.sol --bin --abi -o ./Retailer
```

Compile Contract 2 with:

```bash
solc ./Manufacturer.sol --bin --abi -o ./Manufacturer
```

After compiling both contracts, they should be [**ready for deployment**](./deploying-a-contract)!